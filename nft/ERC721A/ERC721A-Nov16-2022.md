# ERC721A-2022-Nov版本节省gas的细节更新
看本文之前请先阅读[链接](https://github.com/Dapp-Learning-DAO/Dapp-Learning/blob/main/nft/ERC721A/ERC721A.md#erc721a%E7%9A%84%E6%94%B9%E8%BF%9B
)
## 如何节省event燃气费(mint为例)

### 定义
```solidity
event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);
```

### 以前调用
```solidity
emit Transfer(address(0), toMasked, startTokenId);
```
### 节省调用

定义常量: 

_TRANSFER_EVENT_SIGNATURE = `keccak256("Transfer(address,address,uint256)")`

因为这里三个参数都是`索引`, 因此调用`log4`, 并且因为没有其他非索引参数, 这里data部分传入0,0

```solidity
asssembly{
  log4(
      0, // Start of data (0, since no data).
      0, // End of data (0, since no data).
      _TRANSFER_EVENT_SIGNATURE, // Signature.签名是定义的时候生成的, 是transfer(address,address,uint256)这串产生的一个sha256
      0, // `address(0)`.
      toMasked, // `to`.
      startTokenId // `tokenId`.
  )
}
```


## 如何节省修改状态变量燃气费(mint为例)

### 以前调用
```
struct TokenOwnership {
    address addr;
    uint64 startTimestamp;
}
```
```solidity
_balances[to] += quantity;
_numberMinted[to] += quantity;
_ownerships[tokenId].addr = to;
_ownerships[tokenId].startTimestamp  = uint64(block.timestamp);
```

### 节省gas调用
修改数据定义
```
// Mapping owner address to address data.
//
// Bits Layout:
// - [0..63]    `balance`
// - [64..127]  `numberMinted`
// - [128..191] `numberBurned`
// - [192..255] `aux`
mapping(address => uint256) private _packedAddressData;
```
```solidity
// Bits Layout:
// - [0..159]   `addr`
// - [160..223] `startTimestamp`
// - [224]      `burned`
// - [225]      `nextInitialized`
// - [232..255] `extraData`
mapping(uint256 => uint256) private _packedOwnerships;
```
定义常量
uint256 private constant _BITPOS_NUMBER_MINTED = 64;

这里修改了两次状态变量, 但是修改了8条数据:
* numberminted
* balance
* token归属权
* 后面那个token数据是否已经初始化(0和1表示)
* timestamp
* 额外数据from是否为0
* 额外数据to是否为0
* 上一条额外数据
```solidity
// The mask of the lower 160 bits for addresses.
uint256 private constant _BITMASK_ADDRESS = (1 << 160) - 1;

function _mint(address to, uint256 quantity)internal{
  //...
  _packedAddressData[to] += quantity * ((1 << _BITPOS_NUMBER_MINTED) | 1);
  _packedOwnerships[startTokenId] = _packOwnershipData(
      to,
      // 224位BURNED记录为0
      // 225位_nextInitializedFlag(quantity)记录了下一个位置是否已经初始化(如果mint一个,那就是已经初始化, 否则就没有,这里只记录0或者1)
      // 232...255记录_nextExtraData(address(0), to, 0)了额外数据
      _nextInitializedFlag(quantity) | _nextExtraData(address(0), to, 0) 
  );
  //...
}

function _packOwnershipData(address owner, uint256 flags) private view returns (uint256 result) {
    assembly {
        // 一个地址只需要160, 256-160剩下是多余的可用空间        
        owner := and(owner, _BITMASK_ADDRESS) // 这里做一下与运算, 以防万一, 把前面几位清空用来存时间戳
        // `owner | (block.timestamp << _BITPOS_START_TIMESTAMP) | flags`.
        // shl(x,y): 逻辑移位,将y左移x位
        result := or(owner, or(shl(_BITPOS_START_TIMESTAMP, timestamp()), flags))
    }
}
```
举例子,假设`quantity = 10`

`_packedAddressData[to] += 0x好多个零A000000000000000A`

## transfer
1. 如果是连续的mint的nft,要拿到连续的第一个的数据,如果不是连续的,则直接返回当前数据
```solidity
function _packedOwnershipOf(uint256 tokenId) private view returns (uint256 packed) {
        if (_startTokenId() <= tokenId) {
            packed = _packedOwnerships[tokenId];
            // If not burned.
            if (packed & _BITMASK_BURNED == 0) {
                // 如果数据是空,说明是连续mint的,那么向前循环查找
                if (packed == 0) {
                    if (tokenId >= _currentIndex) revert OwnerQueryForNonexistentToken();
                    // Invariant:
                    // There will always be an initialized ownership slot
                    // (i.e. `ownership.addr != address(0) && ownership.burned == false`)
                    // before an unintialized ownership slot
                    // (i.e. `ownership.addr == address(0) && ownership.burned == false`)
                    // Hence, `tokenId` will not underflow.
                    //
                    // We can directly compare the packed value.
                    // If the address is zero, packed will be zero.
                    for (;;) {
                        unchecked {
                            packed = _packedOwnerships[--tokenId];
                        }
                        if (packed == 0) continue;
                        return packed;
                    }
                }
                //否则,说明数据存在并且没有被焚毁, 这里可以跳过循环
                // 这里可以节省2143个gas在那些已经初始化的token转账上(因为不用循环了)
                return packed;
            }
        }
        revert OwnerQueryForNonexistentToken();
    }
```

```solidity
function transferFrom(
    address from,
    address to,
    uint256 tokenId
) public payable virtual override {
    uint256 prevOwnershipPacked = _packedOwnershipOf(tokenId);
    // ...一些授权的代转账操作省略

    // Underflow of the sender's balance is impossible because we check for
    // ownership above and the recipient's balance can't realistically overflow.
    // Counter overflow is incredibly unrealistic as `tokenId` would have to be 2**256.
    unchecked {
        // 2. 取到ownership数据后对balance进行操作(to++,from--)
        --_packedAddressData[from]; // Updates: `balance -= 1`.
        ++_packedAddressData[to]; // Updates: `balance += 1`.

        // Updates:
        // - `address` to the next owner.
        // - `startTimestamp` to the timestamp of transfering.
        // - `burned` to `false`.
        // - `nextInitialized` to `true`.
        // 3. 初始化/修改当前nft的ownership数据
        _packedOwnerships[tokenId] = _packOwnershipData(
            to,
            _BITMASK_NEXT_INITIALIZED | _nextExtraData(from, to, prevOwnershipPacked)
        );

        // If the next slot may not have been initialized (i.e. `nextInitialized == false`) .
        // 4. 如果后一位还没初始化的(ownership的225位数据),则对后面一位nft的ownership数据进行初始化(把1里面找到的数据直接赋值给他)
        if (prevOwnershipPacked & _BITMASK_NEXT_INITIALIZED == 0) {
            uint256 nextTokenId = tokenId + 1;
            // If the next slot's address is zero and not burned (i.e. packed value is zero).
            if (_packedOwnerships[nextTokenId] == 0) {
                // If the next slot is within bounds.
                if (nextTokenId != _currentIndex) {
                    // Initialize the next slot to maintain correctness for `ownerOf(tokenId + 1)`.
                    _packedOwnerships[nextTokenId] = prevOwnershipPacked;
                }
            }
        }
    }
    emit Transfer(from, to, tokenId);
    _afterTokenTransfers(from, to, tokenId, 1);
}
```
## 嵌套的if条件节省gas

### 原来写法
```solidity
if (!_isSenderApprovedOrOwner(approvedAddress, from, _msgSenderERC721A()) && !isApprovedForAll(from, _msgSenderERC721A())) {
  revert TransferCallerNotOwnerNorApproved();
}
```

### 改成嵌套节省20gas
```solidity
// The nested ifs save around 20+ gas over a compound boolean condition.
        if (!_isSenderApprovedOrOwner(approvedAddress, from, _msgSenderERC721A()))
            if (!isApprovedForAll(from, _msgSenderERC721A())) revert TransferCallerNotOwnerNorApproved();
```




